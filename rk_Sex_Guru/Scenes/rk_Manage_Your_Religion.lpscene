WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

SceneStart()

    SexGuruState = Player.getActorVar(rk_SG_SexGuruState) // open yes/no
    rkAreASexGuru = Player.getActorVar(rk_SG_rkAreASexGuru) // yes/no
    SexGuruReputation = Player.getActorVar(rk_SG_SexGuruReputation) // Reputation
    SexGuruDisciple = Player.getActorVar(rk_SG_SexGuruDisciple) // Disciples
    SGIllumination = Player.getActorVar(rk_SG_SexGuruIllumination) // Illumination
    SexGuruGospels = Player.getActorVar(rk_SG_SexGuruGospels)
    discipleattractiontoplayer = Player.getActorVar(rk_discipleattractiontoplayer)
    SGOpenedTempleWebsite = Player.getActorVar(rk_SGOpenedTempleWebsite)
    GPostCount = Player.getActorVar(rk_SG_GPostCount)
    StartChurchCommerce = Player.getActorVar(rk_StartChurchCommerce)
    
    // If the player is dating someone we need to end the date, so we can check this NPCs stats as well
    If CurrentCompanion.isValid()
        Player::"<CurrentCompanion.Name>, I need to take care of my community. See you later!"
        endDate()
    EndIf
    
    // Go to the Temple if any
    TempleSize = Player.getActorVar(rk_PlayerTempleLocationSize)
    If TempleSize > 0
        moveTo(Home)
        setBackground3D(Modules/vin_Base/World/place_of_worship___vin01.lpworld)
    EndIf

        "What should I do?"
        1::"Write a gospel"
        2::"Meditate"
        20:: rkAreASexGuru > 0 && StartChurchCommerce > 0:: "Manage your production"
        10::"Manage inventories"
        50::"Manage community"
        60:: rkAreASexGuru > 0 && StartChurchCommerce > 0:: "Community sex shooting"
        0::"Promote my religion"
        99::"I changed my mind!"
    
        If 0
    
            "How I will promote?"
            1::"Talking about my religion"
            2:: SexGuruGospels >= 2 ::"Do a public presentation"
            70:: rkAreASexGuru > 0 || StartChurchCommerce > 0:: "Promote my religion locally"
            80:: SGOpenedTempleWebsite >= 1 && SexGuruGospels >= 1 ::"Promote my religion online"
            90:: SGOpenedTempleWebsite >= 1 && SexGuruGospels >= 1 ::"Publish content on my website"
            91:: SGOpenedTempleWebsite <= 0 ::"Launch a community website"
            92:: SGOpenedTempleWebsite >= 1 ::"Close your community website"
            99::"Do nothing!"
        
            If 1
        
                "Each time you talk about your religion to someone, you receive a little bonus."
                "Click on a person and click 'Talk about your religion'."
            
            ElseIf 2
        
                followUp(rk_SG_Public_Presentation)

            ElseIf 70
            
                followUp(rk_promoteTemple_locally)
        
            ElseIf 80
        
                followUp(rk_SG_book_online_ads)
        
            ElseIf 90
        
                followUp(rk_SG_upload_website)
        
            ElseIf 91
    
                If SGOpenedTempleWebsite < 1
            
                Tprice = 3599
                TpriceLocal = Tprice.convertToLocalCurrency()
                
                TRweekly = 499
                TRweeklyLocal = TRweekly.convertToLocalCurrency()
                
                TotalTTR = Tprice + TRweekly
                
                Player::"Getting a website up and running for my religious community will cost <TpriceLocal> once and <TRweeklyLocal> per month for administration and hosting. Continue?"
                0:: "Yes"
                1:: "No"
                
                If 0
                    If money >= TotalTTR
                        
                        Player.setActorVar(rk_SGOpenedTempleWebsite, 1)
                        Player::"It's time to write and post some gospels on your blog to attract visitor and get more chance to find new disciples!"
                        Player:SGOpenedTempleWebsite => 1
                        money -= TotalTTR
                        
                    Else
                    
                        Player::"Well with some more money I could afford this. I need to make some more cash first."
                        
                    EndIf
                    
                Else
                
                    Player::"I will wait a bit longer and collect some more gospels to publish online."
                    
                Endif

            ElseIf 92
    
                "Should I stop having a relegious community website for all my followers and visitors?"
                0:: "Yes"
                1:: "No"
    
                If 0
    
                    "Bah, in fact I don't need a website for my community."
                    Player.setActorVar(rk_SGOpenedTempleWebsite, 0)
                
                    "Close the religious community website"
                    Player:SGOpenedTempleWebsite => 0

                Else
    
                    "I was kidding, what was I thinking? Giving up that easy is not my style."
        
                Endif           

            EndIf
    
        ElseIf 1
    
            followUp(rk_SG_Write_a_Gospel)
    
        ElseIf 2
    
            followUp(rk_SG_meditate)
    
        ElseIf 10
    
            followUp(rk_Manage_Temple_Inventory)
    
        ElseIf 20
    
            "What do I want to produce?"
            0::"Create special frozen desserts"
            19::"Create special temple drinks"
            20::"Print X calendars displaying temple's models"
            21::"Print X magazines displaying temple's models"
            31::"Print X movies displaying temple's models"
            99::"Go back!"
        
            If 0
            
                followUp(rk_SGProduce_FrozMeal)
                
            ElseIf 19
            
                followUp(rk_SG_Produce_Drinks)
            
            ElseIf 20
            
                followUp(rk_SG_Produce_Calendar)
            
            ElseIf 21
            
                followUp(rk_SG_Produce_XXXMag)
            
            ElseIf 31
            
                followUp(rk_SG_Produce_XXXVid)
            
            EndIf
    
        ElseIf 50
        
            "What do I want to do?"
            10:: TempleSize > 0 ::"Show donations"
            31:: SGOpenedTempleWebsite >= 1 ::"Analyse visitor interest"
            35:: rkAreASexGuru > 0 ::"Manage employees"
            20::"Quick religion stats" // will be community report
            45:: rkAreASexGuru > 0 ::"Community report"
            80:: rkAreASexGuru > 0 ::"Buy equipment"
            90:: rkAreASexGuru > 0 ::"Rent/Move to a new temple"
            94::"Tutorial"
            95::"Religion Cheats/Reset"
            97::"Close your religious community"
            99::"Go back!"
       
            If 10
        
                followUp(rk_Receive_Donation)
        
            ElseIf 31
        
                followUp(rk_SG_analyse_visitor_interest)
        
            ElseIf 35
    
                followUp(rk_SG_manage_employees)
        
            ElseIf 20
            
                If rkAreASexGuru < 1
            
                    YouAre = "You're not a sex guru yet!"
            
                Else
            
                    YouAre = "Actually a sex guru"
            
                EndIf
                
                If SexGuruState < 1
            
                    RelYes = "not created"
            
                Else
            
                    RelYes = "created"
            
                EndIf
            
                If SGOpenedTempleWebsite < 1
            
                    webYes = "You don't have any website!"
            
                Else
            
                    webYes = "You have a website for your community!"
            
                EndIf
            
                SGPussyJuice = Player.getActorVar(rk_SGPussyJuice)
                SGSpermJuice = Player.getActorVar(rk_SGSpermJuice)
                AmountDons = SGPussyJuice + SGSpermJuice
                If TempleSize == 6
                    Tsize = "You own a very large temple"
                ElseIf TempleSize == 5
                    Tsize = "You own a large temple"
                ElseIf TempleSize == 4
                    Tsize = "You own a spacious temple"
                ElseIf TempleSize == 3
                    Tsize = "You own a adequate temple"
                ElseIf TempleSize == 2
                    Tsize = "You own a small temple"
                Else
                    Tsize = "You don't own a temple"
                EndIf
        
                // Stats
                0::"<YouAre>"
                0::"You have <RelYes> a religion"
                0::"You have <SexGuruDisciple> disciple(s)"
                0::"Your reputation level is <SexGuruReputation>"
                0::"Your disciple attraction level is <discipleattractiontoplayer>"
                0::"Your illumination level is <SGIllumination>"
                0::"<Tsize>"
                0::"<webYes>"
                0::"You have posted <GPostCount> gospel(s) on your website"
                0::"You actually own <SexGuruGospels> gospel(s)"
                0::"You actually own <AmountDons> bottle(s) of sex juice"
                99::"Go back!"
            
            ElseIf 45
            
                followUp(rk_SG_community_report)
            
            ElseIf 80
    
                followUp(rk_Buy_Temple_Equip)
            
            ElseIf 90
    
                followUp(rk_SG_rent_temple)
                
            ElseIf 94
            
                followUp(rk_SG_Tutorial)
                
            ElseIf 95
    
                "What I want to do?"
                0:: "Cheating"
                1:: "Reset all Sex Guru Stats"
                99::"Go back!"
            
                If 0

                    followUp(rk_Cheat_religion)
            
                ElseIf 1
    
                    followUp(rk_reset_religion)
            
                EndIf
    
            ElseIf 97
    
                "Should I stop preaching the gospel and freeing all my followers?"
                0:: "Yes"
                1:: "No"
    
                If 0
    
                    "Bah, in fact I was ripping off people and that's not fair, I guess my religious side is starting to take over my crook side."
                    "Close the religious community"
                    Player:rkOpenedSexGuru => 0
        
                    "I'm not a sex guru anymore!"
                    Player.setActorVar(rk_SG_rkAreASexGuru, 0)
                    Player.setActorVar(rk_PlayerTempleLocationSize, 0)
                    Player.setActorVar(rk_PlayerTemplePrice, 0)
                    Player.setActorVar(rk_SG_SexGuruIllumination, 0)
                    Player.setActorVar(rk_SG_SexGuruState, 0)
                    
                    "Note, that not all of your guru's stats have been removed. If you want to reset Sex-Guru to its original state, click Yes!"
                    "Do I want I want to reset Sex-Guru mod?"
                    0::"Yes"
                    1::"No"
                    
                    If 0
                    
                        followUp(rk_reset_religion)
                    
                    EndIf
        
                Else
    
                    "I was kidding, what was I thinking? Giving up that easy is not my style."
        
                Endif
    
            EndIf
    
        ElseIf 60
    
            followUp(rk_SG_organize_shooting)
    
        EndIf

SceneEnd()