WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()

    discipleattraction = MainActor.getActorVar(rk_discipleattraction)
    SexGuruDisciple = Player.getActorVar(rk_SG_SexGuruDisciple)
    
    // local variables for hiring people...
    TempleJobTitle = ""
    currentDiscipleId = 0
    DiscipleIsGenerated = false
    DiscipleAskingPrice = 0
    DisciplejobBasePayment = 50
    DisciplejobRandomPayment = 100
    DiscipleSkillLvl = 1

    // Find out what to hire...
    hireType = SGHireNewDiscipleJobType.getGlobal()

    If hireType == 1

        TempleJobTitle = "Disciple"
        currentDiscipleId = Player.getActorVar(rk_SG_EmployedDiscipleId)
        DisciplejobBasePayment = 0
        DisciplejobRandomPayment = 0

    Else

        "PANIC!! rk_SG_hire_Disciple.lpscene called with unknown job type (<hireType>)!"

    EndIf

    Player.dress()
    Player.show(0)

    // Test if we already got this employee
    MainActor = getSpecific(currentDiscipleId)

    // Already have a employee of this type?
    If MainActor.isValid()
        MainActor.dress()
        secondScreenIfHidden(MainActor)
        MainActor.show()
        Player::"I already have hired <MainActor.Name> as a <TempleJobTitle>."
        Player::"If I want to hire someone else, I need to fire <MainActor.o> first."

        secondScreen()
        MainActor.hide()
    Else

        // Go find a new one
        MainActor = 0
        Player(Wink)::"Should I ask one of my contacts or hire someone?"
        0:: "Ask one of my contacts"
        1:: "Hire someone else"
        2:: "I don't need a <TempleJobTitle>"

        If 0
            foundEmployee = false
            While !foundEmployee
                selectNPC()
                MainActor = getSelectedNPC()

                // Test if the selected actor is already employed
                If MainActor.isValid()
                    currentSalary = MainActor.getActorVar(rk_SG_DiscipleSalary)
                    If currentSalary > 0
                        // Test if this id still is assigned to an employee slot
                        cannotHire = false
                        If MainActor.getID() == Player.getActorVar(rk_SG_EmployedDiscipleId)
                            cannotHire = true

                            //Sanity check!
                            MainActor.setActorVar(tag_SGDisciple, 1)

                        EndIf

                        If cannotHire
                            Player::"<MainActor.Name> is already working for me. I cannot offer <MainActor.o> another job!"
                            MainActor.hide()
                            MainActor = 0
                        Else
                            foundEmployee = true
                        EndIf
                    Else
                        foundEmployee = true
                    EndIf
                Else
                    // User cancelled
                    foundEmployee = true
                EndIf
            EndWhile
        ElseIf 1
            MainActor = generatePersonTemporary()

            foundEmployee = false
            While !foundEmployee

                MainActor.dress()
                secondScreenIfHidden(MainActor)
                MainActor.show()

                DiscipleSkillLvl = Random(1,100)
                skillText = "has no skill"

                // write the stat that controls the skill level
                If hireType == 1
                    
                    MainActor:discipleattraction => DiscipleSkillLvl

                    skillText = "never heard about us before!"
                    If DiscipleSkillLvl >= 90
                        skillText = "a <MainActor.god_or_goddess> in sex"
                    ElseIf DiscipleSkillLvl >= 80
                        skillText = "very famous, the community, some ask me to let <MainActor.o> join"
                    ElseIf DiscipleSkillLvl >= 65
                        skillText = "famous in the community."
                    ElseIf DiscipleSkillLvl >= 50
                        skillText = "well known in the community for is reading of my gospels to others"
                    ElseIf DiscipleSkillLvl >= 35
                        skillText = "somewhat known in the community, some say is in adoration with my religion"
                    ElseIf DiscipleSkillLvl >= 25
                        skillText = "little known in the community"
                    ElseIf DiscipleSkillLvl >= 10
                        skillText = "very little known in the community"
                    EndIf

                EndIf

                Player::"<MainActor.Name> is available to hire. <MainActor.S> is <skillText>. Invite <MainActor.o> to a job interview?"
                0:: "Yes"
                1:: "No"

                If 0
                    foundEmployee = true
                Else
                    MainActor.hide()
                    MainActor = generatePersonTemporary()
                EndIf
            EndWhile
            actorIsGenerated = true
        EndIf

        // If we found someone for the job interview
        If MainActor.isValid()

            // Go to the Temple if any
            TempleSize = Player.getActorVar(rk_PlayerTempleLocationSize)
            If TempleSize > 0
                moveTo(Home)
                setBackground3D(Modules/vin_Base/World/place_of_worship___vin01.lpworld)
            EndIf

            // get the stat that controls the skill level
            If hireType == 1
                // "Disciple"
                DiscipleSkillLvl = MainActor.getActorVar(rk_discipleattraction)
            EndIf

            askingPrice = DisciplejobBasePayment + (DisciplejobRandomPayment * (Random(0.35, 0.5) + (DiscipleSkillLvl/100)))
            priceLocal = askingPrice.convertToLocalCurrency(true)

            Player::"<MainActor.Name> is available to join and wants to give you <priceLocal> / month, for your wisdom and kindness. Join <MainActor.o>?"
            0:: "Yes, <MainActor.Name> can join"
            1:: "Try to negotiate on donation"
            2:: "No, look for someone else"

            If 0
                // All good
            ElseIf 1
                chance = Random(1,5)
                If chance >= 3
                    askingPrice += (chance*100)
                    If askingPrice < 1000
                        askingPrice = 1000
                    EndIf
                    askingPriceLocal = askingPrice.convertToLocalCurrency(true)
                    Player(Wink)::"I was able to talk <MainActor.Name> to higher to <askingPriceLocal>."
                Else
                    Random
                        Player(Annoyed)::"<MainActor.Name> was not willing to negotiate."
                        Player(Annoyed)::"My negotiations were unlucky."
                        Player(Annoyed)::"<MainActor.Name> says this is <MainActor.p> minimum."
                    EndRandom

                    Player(Annoyed)::"I will have to accept what <MainActor.o> offer or find another <TempleJobTitle> that I can offer."
                    0:: "Accept what <MainActor.s> offer"
                    1:: "Don't accept <MainActor.o> offer"

                    If 1
                        MainActor = 0
                    Else
                        // All good
                    EndIf
                EndIf
            Else
                MainActor = 0
            EndIf
        EndIf

        // Check if MainActor can pay the negotiated salary
        If MainActor.isValid() && MainActor:money < askingPrice
            MainActor(Sad)::"I don't have enough money to pay <MainActor.Name>."
            MainActor(Curious)::"I need make some money first!"
            MainActor = 0
        EndIf

        // It the actor is still valid, all checks were OK, so join him
        If MainActor.isValid()

            If actorIsGenerated
                MainActor.makePermanent()
                exchangeContact(MainActor)
            EndIf

            SGId = MainActor.getID()
            MainActor.setActorVar(rk_SG_DiscipleSalary, askingPrice)
            daysSinceBirth = (Player:Age * 365) // get the current date
            MainActor.setActorVar(rk_SG_LastSalaryNegotiation, daysSinceBirth) // remember last salary negotiation

            // Persist the new employee
            If hireType == 1
                Player.setActorVar(rk_SG_EmployedDiscipleId, SGId)
                MainActor.setActorVar(tag_SGDisciple, 1)
                MainActor.modifyActorVar(rk_discipleattraction, 10)

            Else
                "PANIC!!? rk_SG_hire_employee.lpscene called with unknown job type!"
            EndIf

            Player(Happy)::"<MainActor.Name> and I signed the contract. <MainActor.S> is now working as my <TempleJobTitle>."
            Player(Happy)::"I added <MainActor.Name> to my address book."
        EndIf
    EndIf

    secondScreen()

    passTime(1,2)

    // Go back to the scene that called us...
    followUp(rk_SG_manage_employees)

    SceneEnd()