WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

SceneStart()

    discipleAttraction = MainActor.getActorVar(rk_discipleAttraction)
    SexGuruDisciple = Player.getActorVar(rk_SG_SexGuruDisciple)
    
    // Get the desired location, main location for this code: nn_pe_scene_organize_shooting.lpscene
    SGdesiredShootingLocation = rk_SG_NextShootingLocation.getGlobal()
    
    If SGdesiredShootingLocation == 1 // home
        moveTo(place_of_worship)
    EndIf
    
    If Player.isNaked()
        Player.dress()
    EndIf
    MainActor = CurrentCompanion
    
    // Find a companion
    If MainActor.isValid()
        Player(Happy)::"I'm currently accompanied by <MainActor.Name>. Do I ask <MainActor.o> to be my main actor?"
        0:: "Yes, ask <MainActor.Name> to be my main actor"
        1:: "No, let's check my contacts first"
        99:: "I changed my mind. I don't want to do a shooting"
        
        If 1
            Player::"Listen <MainActor.Name> I have some work to do for the community. Thanks for being with me. See you later!"
            endDate()
            
            selectNPC()
            MainActor = getSelectedNPC()
        ElseIf 99
            MainActor = 0
        EndIf
    Else
        Player(Curious)::"I need a main actor for my shooting. Let's check my contacts."
        selectNPC()
        MainActor = getSelectedNPC()
    EndIf

    If MainActor.isValid() 
        // Check if MainActor is an employee that cannot participate
        salary = MainActor.getActorVar(rk_SG_DiscipleSalary)
        If salary > 0 
            
            camOpId = Player.getActorVar(rk_SG_EmployedPhotographerId)
            sceneId = Player.getActorVar(rk_SG_EmployedFilmakerId)
            currentId = MainActor.getID()
            
            If currentId == camOpId || currentId == sceneId
                
                Player(Sad)::"I need <MainActor.Name> to do the shooting. So <MainActor.s> cannot be the main actor at the same time."
                MainActor = 0
            EndIf
        EndIf
        
        // Check if the actor already did shootings and if not ask to do one
        If MainActor.isValid() && !MainActor.isProstitute() // sex workers won't decline...
        
            hasDoneShootings = MainActor.getActorVar(rk_SG_HasDoneShootings)
            participationState = MainActor.getActorVar(rk_SG_DeclinedDoingShootings)
            
            If hasDoneShootings < 1
                Player(Curious)::"Let's ask <MainActor.name> if <MainActor.s> is interested in a shooting..."
            Else
                Player(Curious)::"Let's ask <MainActor.name> if <MainActor.s> is interested in another shooting."
            EndIf
            
            MainActor.dress()
            MainActor.show(1)
            
            SGisAgencyModel = MainActor.getActorVar(rk_SG_IsAgencyModel)
            
            // If perverted enough will join
            If MainActor:perversion >= 55 || hasDoneShootings > 0 || SGisAgencyModel > 0
                If hasDoneShootings > 0 || SGisAgencyModel > 0
                    If MainActor:perversion >= 55 || SGisAgencyModel > 0
                        Random
                            MainActor(Serene)::"Glad you ask again. I loved the last shooting. Yeah I'm in!"
                            MainActor(Serene)::"I was waiting for you to ask me again. Sure thing I will!"
                            MainActor(Posing)::"One day I will be your most wanted disciple! Let's go."
                        EndRandom
                        MainActor.modifyActorVar(rk_discipleAttraction, 10)
                    Else
                        Random
                            MainActor(Anxious)::"I really hope no one will find out that I am the main actor."
                            MainActor(Shy)::"Somehow I liked what we did last time. I think one more time is OK"
                        EndRandom
                        MainActor.modifyActorVar(rk_discipleAttraction, 5)
                    EndIf
                Else
                    Random
                        MainActor(Sarcastic)::"Why not. My neighbours think I am a total sex accro, so lets make their wish come true."
                        MainActor(Shy)::"Do you think I could do that? Really? Ok let's try..."
                        MainActor(Posing)::"Hm... I always wanted to do a nude shooting. Count me in!"
                        MainActor(Surprised)::"Wow. I am amazed that you think about *me*. I never thought that I am attractive enough for that!"
                        MainActor(Flirty)::"Well, you know I am a little pervert. This is going to be fun!"
                    EndRandom
                    MainActor.modifyActorVar(rk_discipleAttraction, 5)
                EndIf
            Else
                If MainActor:masochist <= -50
                    If participationState == 1
                        Random
                            MainActor(Embarassed)::"You are not giving up easily hm? I am still not convinced."
                            MainActor(Shocked)::"I still don't think that I am the right person for that."
                            MainActor(Shocked)::"I knew you would ask me that again, but I'm not going to do that again."
                            MainActor(Irritated)::"No. Since you asked me last time I have bad dreams about that."
                        EndRandom
                    Else
                        Random
                            MainActor(Shocked)::"I knew you would ask me some time, but I think I am better not doing that."
                            MainActor(Irritated)::"I did that once in my dreams and it had a very bad ending."
                            MainActor(Confused)::"Did you just ask me to get naked? In front of a camera? I never did such thing."
                        EndRandom
                    EndIf
                    
                    Random
                        Player(Curious)::"Seems <Actor.Name> is a little bit shy on that topic."
                        Player(Curious)::"Well that was a NO. But a doubtful one. So maybe next time..."
                        Player(Curious)::"Hmm... <Actor.Name> said 'no' but I feel <Actor.o> meant something else."
                    EndRandom
                    
                    Random
                        Player(Wink)::"Should I try to convince <MainActor.Name> to try it out?"
                        Player(Wink)::"Should I use my charm and talk to <MainActor.Name> to try it out?"
                    EndRandom
                    
                    0:: "Yes"
                    1:: "No"
                    
                    If 0
                        Random
                            Player(Flirty)::"Listen honey, you are so beautiful. Not showing your body to the world would be a sin."
                            Player(Wink)::"This is not going to hurt. If you do not like the results I will simply don't release them online!"
                            Player(Flirty)::"You are such a beautiful and sexy person. You really should give it a try!"
                            Player(Wink)::"There is nothing wrong with being naked in front of a camera. It's like being here but with a camera focused on you."
                            Player(Wink)::"Let's say I would give you some hundreds cash - Still not interested?"
                        EndRandom
                        
                        chance = Random(1,4)
                        
                        If chance >= 3
                            Random
                                MainActor(Excited)::"Ok. You got me. But this is not going to be cheap for you!"
                                MainActor(Shy)::"Hmm... Ok. But I want to see the results before you put everything online."
                                MainActor(Shy)::"You could be right. Ok! Let's do that. But don't tell any of my friends about that!"
                            EndRandom
                            MainActor.modifyActorVar(rk_discipleAttraction, 5)
                        Else
                            Random
                                MainActor(Angry)::"Well you have good points, but I am way too reputable for that."
                                MainActor(Angry)::"I want to stay as a disciple and reserved my body to you!"
                                MainActor(Irritated)::"I think I am not really ready for that. Give me some time..."
                                MainActor(Irritated)::"Uhh... My brain says yes. But my body says no."
                            EndRandom
                            
                            Random
                                Player(Annoyed)::"Ok, this time <MainActor.s> declined. But I should ask <MainActor.o> again some time..."
                                Player(Annoyed)::"Looks like <MainActor.Name> is really too shy at the moment. Let's open <MainActor.p> mind a bit and ask <MainActor.o> again later..."
                                Player(Sad)::"Damn! I want <MainActor.o> to take part in the shootings. I need to try harder. I like <MainActor.o> beautiful body!"
                                Player(Sarcastic)::"I need to find some better arguments to turn <MainActor.Name> into a disciple model. Let me try again later..."
                                Player(Sarcastic)::"Seems <MainActor.Name> really doesn't know what's good for <MainActor.o>. I need to talk to <MainActor.o> again."
                                Player(Sarcastic)::"This time I failed. I need to try again later."
                            EndRandom
                            MainActor.modifyActorVar(rk_discipleAttraction, -5)
                            
                            MainActor.setActorVar(rk_SG_DeclinedDoingShootings, 1)
                            
                            // Not taking part
                            MainActor = 0
                        EndIf
                    Else
                        // Not taking part
                        MainActor = 0
                    EndIf
                Else
                    If participationState == 2  // Already asked!
                        Random
                            MainActor(Angry)::"Are you stupid or what?! I already said NO! - NO, do you understand?!!!"
                            MainActor(Angry)::"OK. I see you are trying hard but: Thanks. I already said NO!"
                            MainActor(Angry)::"NOT IN MY LIFE! - Got it?"
                            MainActor(Irritated)::"Do you remember what I said last time?! No means no."
                        EndRandom
                    Else
                        Random
                            MainActor(Angry)::"What? Do you think I am a pornstar or what? Get out of my face!"
                            MainActor(Angry)::"No. Never and ever. I WILL GOING TO DO THAT."
                            MainActor(Flirty)::"He he! Nice try. Go find someone else!"
                            MainActor(Shocked)::"Really an interesting offer but I am not into these things."
                            MainActor(Angry)::"What? You must be joking. I am a successful business person."
                            MainActor(Shocked)::"Ah come on! Get lost! Go find someone else!"
                        EndRandom
                    EndIf
                    
                    Random
                        Player(Annoyed)::"All right, seems <MainActor.Name> really is not going to shoots for me."
                        Player(Annoyed)::"Oops... I better find someone else to shoots for me..."
                        Player(Sad)::"Damn! That was a definitive 'NO'. Luckily there are more people around."
                        Player(Sad)::"Bad for me. I will have to look for someone else."
                        Player(Sarcastic)::"Hah! If <MainActor.s> does not know whats good for <MainActor.o>! I will go find someone else."
                    EndRandom
                    MainActor.modifyActorVar(rk_discipleAttraction, -10)
                    
                    // Not taking part
                    MainActor.setActorVar(rk_SG_DeclinedDoingShootings, 2)
                    MainActor = 0
                EndIf
            EndIf
        EndIf
    EndIf

    // If an actor was found jump to the next scene
    If MainActor.isValid()
        // does porn...
        MainActor.setActorVar(rk_SG_DeclinedDoingShootings, 99)
        MainActor.setActorVar(rk_SG_HasDoneShootings, 1)
        MainActor.setActorVar(tag_SGDisciple, 1)
        Player.modifyActorVar(rk_SG_SexGuruDisciple, 1)
        MainActor.passOn()
    Else
        // TODO: This is a 'hack' to prevent passing on a previously selected actor
        0.passOn()
        
    EndIf
    
    followUp(rk_SG_nego_shooting_price_disciple_model)
    
SceneEnd()